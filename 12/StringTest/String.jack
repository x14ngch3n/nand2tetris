// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/String.jack

/**
 * Represents character strings. In addition for constructing and disposing
 * strings, the class features methods for getting and setting individual
 * characters of the string, for erasing the string's last character,
 * for appending a character to the string's end, and more typical
 * string-oriented operations.
 */
class String {

    field int length;
    field Array chars;

    /** constructs a new empty string with a maximum length of maxLength
     *  and initial length of 0. */
    constructor String new(int maxLength) {
        let length = 0;
        let chars = Array.new(maxLength + 1);
        let chars[maxLength] = 0; // null terminator
        return this;
    }

    /** Disposes this string. */
    method void dispose() {
        do chars.dispose();
        return;
    }

    /** Returns the current length of this string. */
    method int length() {
        return length;
    }

    /** Returns the character at the j-th location of this string. */
    method char charAt(int j) {
        return chars[j];
    }

    /** Sets the character at the j-th location of this string to c. */
    method void setCharAt(int j, char c) {
        let chars[j] = c;
        return;
    }

    /** Appends c to this string's end and returns this string. */
    method String appendChar(char c) {
        let chars[length] = c;
        let length = length + 1;
        return this;
    }

    /** Erases the last character from this string. */
    method void eraseLastChar() {
        let length = length - 1;
        let chars[length] = 0;
        return;
    }

    /** Returns the integer value of this string, 
     *  until a non-digit character is detected. */
    method int intValue() {
        var int val, i, digit;
        var boolean isMinus;

        let isMinus = false;
        let val = 0;
        let i = 0;
        if (charAt(0) = 45) {
            let isMinus = true;
            let i = i + 1;
        }
        while (i < length) {
            let digit = charAt(i) - 48;
            let val = val * 10 + digit;
            let i = i + 1;
        }
        if (isMinus) {
            let val = -val;
        }
        return val;
    }

    /** Sets this string to hold a representation of the given value. */
    method void setInt(int val) {
        var int lastDigit, c;

        let length = 0;
        if (val < 0) {
            do appendChar(45);
        }
        do setInt_abs(Math.abs(val));
        return;
    }

    /** Returns the new line character. */
    function char newLine() {
        return 128;
    }

    /** Returns the backspace character. */
    function char backSpace() {
        return 129;
    }

    /** Returns the double quote (") character. */
    function char doubleQuote() {
        return 34;
    }

    /** below are helper functions */

    method void setInt_abs(int val) {
        var int lastDigit, c;

        let lastDigit = val - ((val / 10) * 10);
        let c = lastDigit + 48;
        if (val < 10) {
            do appendChar(c);
        } else {
            do setInt_abs(val / 10);
            do appendChar(c);
        }
        return;
    }
}
